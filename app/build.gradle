plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

kotlin {
    jvmToolchain(11)
}

android {
    compileSdk = libs.versions.android.compileSdk.get().toInteger()
    namespace = "org.androidaudioplugin.ports.juce.os251"

    defaultConfig {
        applicationId "org.androidaudioplugin.ports.juce.os251"
        minSdk = libs.versions.android.minSdk.get().toInteger()
        ndk {
            // should we make it customizable? We skip others just to reduce build time.
            abiFilters "x86_64", "arm64-v8a"
        }
        externalNativeBuild {
            cmake {
                arguments "-DANDROID=1",
                        "-DAAP_DIR=${System.env.AAP_DIR != null ? System.env.AAP_DIR : "$projectDir/../external/aap-core"}",
                        "-DAAP_JUCE_DIR=${System.env.AAP_JUCE_DIR != null ? System.env.AAP_JUCE_DIR : "$projectDir/../external/aap-juce"}",
                        "-DANDROID_STL=c++_shared", "-DANDROID_CPP_FEATURES=exceptions rtti"
            }
        }
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }

    ndkVersion libs.versions.ndk.get()
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.22.1"
        }
    }
    buildFeatures {
        prefab true
    }

    sourceSets {
        main.java.srcDirs +=
            ["../external/OS-251/lib/JUCE/modules/juce_gui_basics/native/javaopt/app"]
        main.res.srcDirs +=
            []
    }

    dependencies {
        implementation libs.androidx.core.ktx
        implementation libs.startup.runtime
        implementation libs.aap.core
        implementation libs.aap.midi.device.service
        implementation libs.aap.ui.web
        implementation libs.aap.ui.compose
    }
}
